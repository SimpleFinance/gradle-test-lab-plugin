import com.simple.gradle.testlab.model.Orientation

plugins {
    id 'com.android.application' version '3.3.2'
    id 'com.simple.gradle.testlab' version '2.0-SNAPSHOT'
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
}

repositories {
    google()
    jcenter()
}

dependencies {
    androidTestImplementation 'androidx.test:core:1.0.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
}

testLab {
    googleApi {
        bucketName = "my-bucket-name"
        serviceCredentials = file("/path/to/service/credentials.json")
        projectId = "my-project-id-12345"
    }

    tests {
        instrumentation("instrumented") {
            environmentVariables["key"] = "value"
            testRunnerClass = "com.example.test.MyTestRunner"
            testTargets.addAll(
                    "package com.my.package.name",
                    "notPackage com.package.to.skip",
                    "class com.foo.ClassName",
                    "notClass com.foo.ClassName#testMethodToSkip",
                    "annotation com.foo.AnnotationToRun",
                    "size large notAnnotation com.foo.AnnotationToSkip"
            )
            artifacts {
                all()
                instrumentation = true
                junit = true
                logcat = true
                video = true
            }
            additionalApks.from(
                    "/path/to/some.apk",
                    "/path/to/another.apk"
            )
            disablePerformanceMetrics = true
            disableVideoRecording = true
            resultsHistoryName = provider {
                def prNumber = "1234"
                "GitHub PR $prNumber"
            }
            testTimeout = "600s"
            directoriesToPull.addAll(
                    "/sdcard/path/to/dir",
                    "/data/local/tmp/path/to/dir"
            )
            networkProfile = "LTE"
            device {
                model = "hammerhead"
                api = 21
                locale = "en"
                orientation = Orientation.PORTRAIT
            }
        }

        robo("foo") {
            appInitialActivity = "com.example.app.MainActivity"
            maxDepth = 50
            maxSteps = 200

            artifacts {
                all()
                logcat = true
                screenshots = true
                video = true
            }

            directives {
                click("login_button")
                text("username", "alice")
            }

            additionalApks.from(
                    "/path/to/some.apk",
                    "/path/to/another.apk"
            )
            disablePerformanceMetrics = true
            disableVideoRecording = true
            resultsHistoryName = provider {
                def prNumber = "1234"
                "GitHub PR $prNumber"
            }
            testTimeout = "600s"
            account.googleAuto()
            directoriesToPull.addAll(
                    "/sdcard/path/to/dir",
                    "/data/local/tmp/path/to/dir"
            )
            networkProfile = "LTE"
            device {
                model = "hammerhead"
                api = 21
                locale = "en"
                orientation = Orientation.PORTRAIT
            }
        }
    }
}
